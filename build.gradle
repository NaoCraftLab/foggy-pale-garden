plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
}

version = project.modVersion
group = project.group

base {
    archivesName = "${project.modId}-${project.minecraftReleaseVersion}"
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraftFirstSnapshotVersion}"
    mappings "net.fabricmc:yarn:${project.yarnMappingsVersion}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loaderMinVersion}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand(
                modId: project.findProperty("modId"),
                modVersion: project.findProperty("modVersion"),
                modName: project.findProperty("modName"),
                modDescription: project.findProperty("modDescription"),
                modLicense: project.findProperty("modLicense"),
                modHomepage: project.findProperty("modHomepage"),
                modSources: project.findProperty("modSources"),
                modIssueTracker: project.findProperty("modIssueTracker"),
                modDiscord: project.findProperty("modDiscord"),
                loaderMinVersion: project.findProperty("loaderMinVersion"),
                minecraftJavaVersion: project.findProperty("minecraftJavaVersion"),
                minecraftFirstSnapshotFullVersion: project.findProperty("minecraftFirstSnapshotFullVersion")
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = minecraftJavaVersion.toInteger()
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}
